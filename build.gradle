buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            // Add maven repo for SpecialSource
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:1.2'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'de.undercouch.download'

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

repositories {
    maven {
    name = "forge"
    url = "http://files.minecraftforge.net/maven"
    }
    maven { // the repo from which to get NEI and stuff
    name 'CB Repo'
    url "http://chickenbones.net/maven"
    }
    ivy {
    name 'FMP'
    artifactPattern "http://files.minecraftforge.net/[module]/[module]-dev-[revision].[ext]"
    }
}

dependencies {
    compile group: 'codechicken', name: 'NotEnoughItems', version: "${config.version.minecraft}-${config.version.nei}", ext: 'jar', classifier: "dev"
    compile group: 'codechicken', name: 'CodeChickenCore', version: "${config.version.minecraft}-${config.version.cccore}", ext: 'jar', classifier: "dev"
    compile fileTree(dir: 'libs', include: '*.jar')
    compile fileTree(dir: 'libs', include: '*.zip')
}

group = "particlephysics"
archivesBaseName = "ParticlePhysics"

minecraft {
    version = "${config.version.forge}-${config.version.minecraft}"

    replaceIn "AlchemyPlusPlus.java"
    replace "@MAJOR@", config.version.mod.major
    replace "@MINOR@", config.version.mod.minor

    if (System.getenv("BUILD_NUMBER") != null) {
        replace "@BUILD@", System.getenv("BUILD_NUMBER")
    }
    else
    {
        replace "@BUILD@", "ManuallyBuilt"
    }
}

processResources {
    from 'build.properties'
}

allprojects {
    version = "${config.version.minecraft}-${config.version.mod.major}.${config.version.mod.minor}"

    if (System.getenv("BUILD_NUMBER") != null) {
        version += ".${System.getenv("BUILD_NUMBER")}"
    } else {
        version += "-ManuallyBuilt"
    }

    jar {
        destinationDir = file (rootProject.getRootDir().getPath() + '/output')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact jar
            }
        }
        repositories {
            maven {
                url "file://var/www/maven"
            }
        }
    }
}

import de.undercouch.gradle.tasks.download.Download

task getLibraries {
    def libsFolder = new File('libs')
    if (!libsFolder.isDirectory() ) {
        libsFolder.mkdirs()
    }

    def cofhSource = 'http://minecraft.curseforge.com/mc-mods/220333-cofhlib/files/2236777/download'
    def cofhDest = new File('libs', 'CoFHLib-1.7.10-1.0.2-160-dev.jar')

    if(!cofhDest.exists() ){
        download {
            src cofhSource
            dest cofhDest
        }
    }

}

build.dependsOn "getLibraries"
